/*
 * Open Source Software published under the Apache Licence, Version 2.0.
 */

import org.gradle.internal.os.OperatingSystem

ext {
    operatingSystem = OperatingSystem.current()
}

allprojects {
    group 'io.github.vocabhunter'
}

wrapper {
    description 'Setup the Gradle wrapper'
    gradleVersion = '4.10.2'
    distributionUrl = "https://services.gradle.org/distributions/gradle-${gradleVersion}-all.zip"
}

buildscript {
    repositories {
        jcenter()
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.20.0'
        classpath 'net.researchgate:gradle-release:2.8.0'
        classpath 'org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:2.6.2'
        classpath 'gradle.plugin.com.github.spotbugs:spotbugs-gradle-plugin:1.6.8'
    }
}

apply plugin: 'base'
apply plugin: 'net.researchgate.release'
apply plugin: 'org.sonarqube'

subprojects {

    apply plugin: 'java'
    apply plugin: 'pmd'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.spotbugs'
    apply plugin: 'jacoco'
    apply plugin: 'com.github.ben-manes.versions'

    sourceCompatibility = 11
    targetCompatibility = 11

    repositories {
        jcenter()
    }

    dependencies {
        testCompile 'org.junit.jupiter:junit-jupiter-api:5.3.2'
        testCompile 'org.junit.jupiter:junit-jupiter-params:5.3.2'
        testRuntime 'org.junit.jupiter:junit-jupiter-engine:5.3.2'
        testCompile 'org.mockito:mockito-junit-jupiter:2.23.4'
        runtime 'ch.qos.logback:logback-classic:1.2.3'
    }

    test {
        useJUnitPlatform()
    }

    pmd {
        toolVersion = '6.10.0'
        ruleSetFiles = files("$rootProject.projectDir/config/pmd/ruleset.xml")
        ruleSets = []
    }

    checkstyle {
        toolVersion = '8.16'
        configFile = file("$rootProject.projectDir/config/checkstyle/checkstyle.xml")
    }

    [checkstyleMain, checkstyleTest].each { task ->
        task.logging.setLevel(LogLevel.LIFECYCLE)
    }

    spotbugs {
        toolVersion = '3.1.10'
        effort = 'max'
        reportLevel = 'medium'
        excludeFilter = file("$rootProject.projectDir/config/spotbugs/excludeFilter.xml")
    }

    tasks.withType(com.github.spotbugs.SpotBugsTask) {
        reports {
            xml.enabled = false
            html.enabled = true
        }
    }

    jacoco {
        toolVersion = '0.8.2'
    }

    jacocoTestReport {
        reports {
            xml.enabled true
            xml.destination file("${buildDir}/reports/jacoco/report.xml")
        }
    }

    [compileJava, compileTestJava].each { task ->
        task.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation" << "-Werror"
    }
}
