/*
 * Open Source Software published under the Apache Licence, Version 2.0.
 */
import org.apache.tools.ant.filters.*

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.8'
}

mainClassName = "io.github.vocabhunter.gui.main.VocabHunterGuiExecutable"

applicationName = "VocabHunter"

ext.buildTimestamp = new Date().format('yyyy-MM-dd HH:mm:ss')

processResources {
    filesMatching('properties/*.properties') {
        filter ReplaceTokens, tokens: [
            'build.version': project.property("version"),
            'build.timestamp': project.buildTimestamp
        ]
    }
}

jar {
    from(buildDir) {
        include 'build-info.properties'
    }
}

startScripts {
    doLast {
        unixScript.text = unixScript.text
                .replaceAll('(?m)^APP_HOME=', 'export APP_HOME=')
        windowsScript.text = windowsScript.text
                .replaceAll(/(set CLASSPATH=%APP_HOME%\\lib\\).*/, { "${it[1]}*" })
    }
}

dependencies {
    compile project(':core')
    compile 'com.gluonhq:ignite-guice:1.0.2'
    compile 'org.controlsfx:controlsfx:11.0.0'
    compile 'de.jensd:fontawesomefx-fontawesome:4.7.0-9.1.2'

    // This is required for the conditional logic in the logback setup
    runtime 'org.codehaus.janino:janino:3.0.15'

    testCompile project(path: ':core', configuration: 'tests')

    testCompile 'org.testfx:testfx-junit5:4.0.16-alpha'

    testRuntime 'org.testfx:openjfx-monocle:jdk-11+26'
}

javafx {
    modules = [
        'javafx.controls', 'javafx.fxml'
    ]
    version = '11.0.2'
}

test {
    if (project.hasProperty("skipGuiTests")) {
        exclude 'io/github/vocabhunter/gui/main/GuiTest*'
    }
    if (!project.hasProperty("noHeadless")) {
        jvmArgs "-Dheadless=true"
    }
}

task copyDependencies(type: Copy) {
    from configurations.runtime
    from jar
    into "${buildDir}/libraries"
}

task copyJavafxModules(type: Copy) {
    from configurations.runtimeClasspath
    include "javafx*${javafx.platform.classifier}.jar"
    into "${buildDir}/javafx-modules"
}
