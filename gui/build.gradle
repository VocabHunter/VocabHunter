/*
 * Open Source Software published under the Apache Licence, Version 2.0.
 */

apply plugin: 'application'

mainClassName = "VocabHunter"

applicationName = "VocabHunter"

ext.buildTimestamp = new Date().format('yyyy-MM-dd HH:mm:ss')

task createBuildInfoFile(dependsOn: processResources) << {
    def buildInfoFile = new File("$buildDir/build-info.properties")
    Properties props = new Properties()
    props.setProperty('version', project.property("version"))
    props.setProperty('timestamp', project.buildTimestamp)
    props.store(buildInfoFile.newWriter(), null)
}

jar {
    dependsOn createBuildInfoFile
    from(buildDir) {
        include 'build-info.properties'
    }
}

startScripts {
    doLast {
        unixScript.text = unixScript.text
                .replaceAll('(?m)^APP_HOME=', 'export APP_HOME=')
        windowsScript.text = windowsScript.text
                .replaceAll(/(set CLASSPATH=%APP_HOME%\\lib\\).*/, { "${it[1]}*" })
    }
}

dependencies {
    compile project(':core')
    compile 'org.picocontainer:picocontainer:2.15'
    testCompile 'org.hamcrest:hamcrest-all:1.3'
    testCompile project(path: ':core', configuration: 'tests')
    testCompile 'org.testfx:testfx-core:4.0.2-alpha'
    testCompile 'org.mockito:mockito-all:1.10.19'
    testRuntime 'org.jfxtras:openjfx-monocle:1.8.0_20'
    runtime 'org.codehaus.janino:janino:2.7.8'
}

test {
    if (project.hasProperty("skipGuiTests")) {
        exclude 'io/github/vocabhunter/gui/main/GuiTest*'
    }
    jvmArgs "-Dheadless=true"
}
