/*
 * Open Source Software published under the Apache Licence, Version 2.0.
 */
import org.apache.tools.ant.filters.*

apply plugin: 'application'

mainClassName = "io.github.vocabhunter.gui.main.VocabHunterGuiExecutable"

applicationName = "VocabHunter"

ext.buildTimestamp = new Date().format('yyyy-MM-dd HH:mm:ss')

processResources {
    filesMatching('properties/*.properties') {
        filter ReplaceTokens, tokens: [
            'build.version': project.property("version"),
            'build.timestamp': project.buildTimestamp
        ]
    }
}

jar {
    from(buildDir) {
        include 'build-info.properties'
    }
}

startScripts {
    doLast {
        unixScript.text = unixScript.text
                .replaceAll('(?m)^APP_HOME=', 'export APP_HOME=')
        windowsScript.text = windowsScript.text
                .replaceAll(/(set CLASSPATH=%APP_HOME%\\lib\\).*/, { "${it[1]}*" })
    }
}
def platform
if (operatingSystem.isWindows()) {
    platform = 'win'
} else if (operatingSystem.isLinux()) {
    platform = 'linux'
} else if (operatingSystem.isMacOsX()) {
    platform = 'mac'
}

configurations {
    javafxCompile
}

dependencies {
    compile project(':core')
    compile 'com.gluonhq:ignite-guice:1.0.2'
    compile 'org.controlsfx:controlsfx:9.0.0'
    compile 'de.jensd:fontawesomefx-fontawesome:4.7.0-9.1.2'

    // This is required for the conditional logic in the logback setup
    runtime 'org.codehaus.janino:janino:3.0.12'

    testCompile project(path: ':core', configuration: 'tests')

    testCompile 'org.testfx:testfx-core:4.0.15-alpha'

    testRuntime 'org.testfx:openjfx-monocle:jdk-11+26'

    javafxCompile "org.openjfx:javafx-base:11.0.2:${platform}"
    javafxCompile "org.openjfx:javafx-graphics:11.0.2:${platform}"
    javafxCompile "org.openjfx:javafx-controls:11.0.2:${platform}"
    javafxCompile "org.openjfx:javafx-fxml:11.0.2:${platform}"

    compile configurations.javafxCompile
}

test {
    if (project.hasProperty("skipGuiTests")) {
        exclude 'io/github/vocabhunter/gui/main/GuiTest*'
    }
    if (!project.hasProperty("noHeadless")) {
        jvmArgs "-Dheadless=true"
    }
}

run {
    classpath = classpath - configurations.javafxCompile
    jvmArgs += [
        '--module-path', configurations.javafxCompile.asPath,
        '--add-modules', 'javafx.controls',
        '--add-modules', 'javafx.fxml',
        '--add-exports', 'javafx.base/com.sun.javafx.runtime=ALL-UNNAMED'
    ]
}

task copyDependencies(type: Copy) {
    from configurations.runtime - configurations.javafxCompile
    from jar
    into "${buildDir}/libraries"
}
