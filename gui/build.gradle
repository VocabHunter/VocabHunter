/*
 * Open Source Software published under the Apache Licence, Version 2.0.
 */

plugins {
    id 'application'
    id 'org.openjfx.javafxplugin' version '0.0.10'
}

mainClassName = "io.github.vocabhunter.gui.main.VocabHunterGuiExecutable"

applicationName = "VocabHunter"

ext.buildTimestamp = new Date().format('yyyy-MM-dd HH:mm:ss')

processResources {
    filesMatching('properties/*.properties') {
        expand(
            buildVersion: project.property("version"),
            buildTimestamp: project.buildTimestamp
        )
    }
}

dependencies {
    implementation project(':core')
    implementation ('com.gluonhq:ignite-guice:1.0.2') {
        exclude group: 'com.google.inject', module: 'guice'
    }
    implementation 'com.google.inject:guice:5.0.1'
    implementation 'org.controlsfx:controlsfx:11.1.0'
    implementation 'de.jensd:fontawesomefx-fontawesome:4.7.0-9.1.2'

    // This is required for the conditional logic in the logback setup
    runtimeOnly 'org.codehaus.janino:janino:3.1.4'

    testImplementation project(':test-utils')

    testImplementation 'org.testfx:testfx-junit5:4.0.16-alpha'
    testImplementation 'ch.qos.logback:logback-classic:1.2.3'

    testRuntimeOnly 'org.testfx:openjfx-monocle:jdk-12.0.1+2'
}

javafx {
    modules = [
        'javafx.controls', 'javafx.fxml'
    ]
    version = '13.0.2'
}

test {
    if (project.hasProperty("skipGuiTests")) {
        exclude 'io/github/vocabhunter/gui/main/GuiTest*'
    }
    if (!project.hasProperty("noHeadless")) {
        jvmArgs "-Dheadless=true"
    }
}

task copyDependencies(type: Copy) {
    from configurations.runtimeClasspath
    from jar
    exclude "javafx-*.jar"
    into "${buildDir}/libraries"
}

task copyJavafxModules(type: Copy) {
    from configurations.runtimeClasspath
    include "javafx-*${javafx.platform.classifier}.jar"
    into "${buildDir}/javafx-modules"
}
